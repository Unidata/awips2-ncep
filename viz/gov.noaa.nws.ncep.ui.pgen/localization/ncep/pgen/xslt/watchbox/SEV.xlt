<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:import href="getIssueTime.xlt"/>
<xsl:import href="getCountiesInState.xlt"/>
<xsl:import href="getStateFullName.xlt"/>
<xsl:output method = "text"/>

<!--
        SEV.xlt
                - generate Watch Box SEV product 

        Change Log:

        B. Yin/Chugach  03/10   Initial Coding
        B. Yin/SGT		12/13	TTR 904 change county list string
        
-->

<xsl:variable name="newline"><xsl:text>
 </xsl:text></xsl:variable>

  <xsl:template match="WatchBox">
	  <xsl:value-of select="$newline"/>
          <xsl:text>WWUS50 KWNS </xsl:text>
          <xsl:call-template name="getIssueTime"/>

	  <xsl:value-of select="$newline"/>
          <xsl:text>SEV</xsl:text>
          <xsl:value-of select="@watchNumber"/>

	  <xsl:value-of select="$newline"/>
	  <xsl:value-of select="$newline"/>
          <xsl:text>.</xsl:text> 

          <xsl:if test="@issueStatus = 'Test'">
                <xsl:text> TEST </xsl:text>
          </xsl:if>
          
      <xsl:value-of select="@watchType"/>
	  <xsl:text> WATCH #</xsl:text>
          <xsl:value-of select="@watchNumber"/>
          <xsl:text> HAS BEEN ISSUED BY THE NWS STORM PREDICTION CENTER</xsl:text>

	  <xsl:value-of select="$newline"/>
	  <xsl:value-of select="$newline"/>
          <xsl:text>EFFECTIVE </xsl:text>
          <xsl:value-of select="@issueTime"/>
          <xsl:text> UNTIL </xsl:text>
          <xsl:value-of select="@expTime"/>

	  <xsl:value-of select="$newline"/>
          <xsl:text>$$</xsl:text>
	  
	<!-- get watch counties in each state-->
	  <xsl:for-each select="States">
		<xsl:variable name="stName">
			<xsl:call-template name="getStateFullName">
                		<xsl:with-param name="st"  select="."/>
			</xsl:call-template>
		</xsl:variable>

		<xsl:if test="not($stName='COASTAL WATERS')">	
			<xsl:value-of select="$newline"/>
                	<xsl:value-of select="$newline"/>
                	<xsl:value-of select="."/>
                	<xsl:value-of select="$newline"/>
                	<xsl:text>. </xsl:text>
                	<xsl:value-of select="$stName"/>
			<xsl:choose>
		  	   <xsl:when test="$stName='LOUISIANA'">
				<xsl:text> PARISHES INCLUDED ARE </xsl:text>
		  	   </xsl:when>
		  	   <xsl:otherwise>
				<xsl:text> COUNTIES INCLUDED ARE </xsl:text>
		  	   </xsl:otherwise>
			</xsl:choose>                	<xsl:value-of select="$newline"/>
                	<xsl:value-of select="$newline"/>
                        <xsl:call-template name="getCountiesInState">
                               	<xsl:with-param name="state" select="."/>
                        </xsl:call-template>
                	<xsl:value-of select="$newline"/>
                	<xsl:text>$$</xsl:text>
		</xsl:if>
           </xsl:for-each>
	
  </xsl:template>
</xsl:stylesheet>
  
